apply plugin: "com.android.library"
apply plugin: "org.jetbrains.kotlin.android"
apply plugin: "com.facebook.react"
apply plugin: "maven-publish"

react {
    // @FIXME standalone/framework dependency clash
    // autolinkLibrariesWithApp()
}

repositories {
    mavenLocal()
    google()
    mavenCentral()
}

android {
    ndkVersion rootProject.ext.ndkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion
    compileSdk rootProject.ext.compileSdkVersion

    namespace "com.bridge"
    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        debug {
            minifyEnabled false
        }
        release {
            minifyEnabled false
        }
    }
    packagingOptions {
        resources {
            excludes += "/META-INF/{AL2.0,LGPL2.1}"
            excludes += "/META-INF/versions/**"
        }
    }
}

dependencies {
    api "com.facebook.react:react-android:0.79.0"
    api "com.facebook.react:hermes-android:0.79.0"

    // start: MANUAL LINKING
    api "com.onfido.reactnative.sdk:onfido-react-native-sdk:14.4.0"
    api "com.callstack.repack:callstack-repack:5.0.6"
    api "com.th3rdwave.safeareacontext:react-native-safe-area-context:5.4.0"
    api "com.swmansion.rnscreens:react-native-screens:4.10.0"
    // end: MANUAL LINKING
}

publishing {
    publications {
        release(MavenPublication) {
            groupId = "com.bridge"
            artifactId = "bridge"
            version = "1.0.0"

            afterEvaluate { from components.release }
        }
    }
    repositories {
        mavenLocal()
    }
}

afterEvaluate {
    tasks.named("releaseSourcesJar") {
        dependsOn("generateCodegenArtifactsFromSchema")
    }
}